// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // 1. Logic for A Register
    Not(in=instruction[15], out=isAInstruction);
    Or(a=isAInstruction, b=instruction[5], out=loadA);

    // 2. First Mux
    Mux16(a=instruction, b=aluOutput, sel=instruction[15], out=outMux1);

    // 3. A Register
    ARegister(in=outMux1, load=loadA, out=outA, out[0..14]=addressM);

    // 4. Second Mux
    Mux16(a=outA, b=inM, sel=instruction[12], out=outAM);

    // 5. D Register
    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=aluOutput, load=loadD, out=outD);

    // 6. ALU
    ALU(x=outD, y=outAM, 
        zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], 
        f=instruction[7], no=instruction[6], 
        out=aluOutput, out=outM, zr=zrOut, ng=ngOut);

    // 7. Jump Logic
    Or(a=zrOut, b=ngOut, out=notPos);
    Not(in=notPos, out=posOut);
    
    And(a=instruction[0], b=posOut, out=jgt); // out > 0
    And(a=instruction[1], b=zrOut, out=jeq);  // out = 0
    And(a=instruction[2], b=ngOut, out=jlt);  // out < 0
    
    Or(a=jgt, b=jeq, out=jge);
    Or(a=jge, b=jlt, out=jumpCondMet);
    
    // Only jump if C Instruction
    And(a=instruction[15], b=jumpCondMet, out=doJump);

    // 8. Program Counter
    PC(in=outA, load=doJump, inc=true, reset=reset, out[0..14]=pc);

    // 9. WriteM for RAM
    And(a=instruction[15], b=instruction[3], out=writeM);
}